model{
  # IPM -- Built for the Starkey elk population

  # Parameters to estimate:
  # R: recruitment
  # S: survival
  # N: abundance
  
  # indexing follows [age, sex, year] unless otherwise specified
  # e.g: N[a,s,yr] -- abundance of age class a, sex s, in year yr
  # ages:
  #   [1,,]: 0.5 yr -- calf
  #   [2,,]: 1.5 yr -- subadult
  #   [3,,]: 2.5 yr -- adult - young
  #   [4,,]: 3.5+yr -- adult - mature
  # sex:
  #   [,1,]: female
  #   [,2,]: male
  
  # Estimated parameters are *CAPITALIZED*
  # Data objects are *lowercase*
  # Exceptions are intermediate parameters (e.g. random effects)
  

  #Process Model================================================================
  
  for(t in 2:nyr){ #t (year)
    ##### Recruitment #####
    # R indexing follows [age,yr]  . . .  males rarely get pregnant
    # Assumes equal sex ratio from birth to age 0.5 years
    
    Ne[1,1,t] <- R[t] * sum(N[2:4,1,t]) * 0.5
    N[1,1,t] ~ dnorm(Ne[1,1,t], abs(1/Ne[1,1,t]))T(Nc_obs[t],)
    N[1,2,t] <- N[1,1,t]
    
    for(x in 1:2){ # x (sex)
      ##### Survival 
      # Sexes have separate survivals except in 0.5 to 1.5 transition
      
      # Estimated proportion of aults in each age class
      prop[1,x,t] <- 0
      prop[2,x,t] <- N[1,x,t-1] / sum(N[1:4,x,t-1])
      prop[3,x,t] <- N[2,x,t-1] / sum(N[1:4,x,t-1])
      prop[4,x,t] <- sum(N[3:4,x,t-1]) / sum(N[1:4,x,t-1])
      
      # Minimum known and number moved
      for(a in 1:4){ # a (age) 
        Nmin[a,x,t] <- Na_obs[x,t] * prop[a,x,t] + Nhar[a,x,t]
        Nmov[a,x,t] <- Na_mov[x,t] * prop[a,x,t]
      } # a (age)
      
      # Ne -- expected abundances
      Ne[2,x,t] <- (N[1,x,t-1] + Nc_mov[x,t]) * S[2,x,t]
      Ne[3,x,t] <- (N[2,x,t-1] + Nmov[2,x,t]) * S[3,x,t]
      Ne[4,x,t] <- (sum(N[3:4,x,t-1]) + sum(Nmov[3:4,x,t])) * S[4,x,t]
      
      ##### Abundance #####
      for(a in 2:4){ # a (age)
        tau_e[a,x,t] <- abs(1 / ((Ne[a,x,t]) * (1 - S[a,x,t]) + 1))
        Naug[a,x,t] ~ dnorm(Ne[a,x,t], tau_e[a,x,t])T(Nmin[a,x,t], )
        N[a,x,t] <- Naug[a,x,t] - Nhar[a,x,t]
      } # a (age)
    } # x (sex)
    
    # Derived values from process each year
    # Add whatever totals are interesting
    N_c[t]    <- N[1,1,t] + N[1,2,t]
    N_yf[t]   <- N[2,1,t]
    N_ym[t]   <- N[2,2,t]
    N_af[t]   <- N[3,1,t] + N[4,1,t]
    N_am[t]   <- N[3,2,t] + N[4,2,t]
    N_f[t]    <- N[3,1,t] + N[4,1,t] + N[2,1,t]
    N_m[t]    <- N[3,2,t] + N[4,2,t] + N[2,2,t]
    N_tot[t]  <- sum(N[,,t])
  } # t
  
  
  #Observation Models===========================================================
  
  # Data are indexed by row (i), with one observation per row
  # The columns for the data follow [year, age, sex, mean, tau]
    # data[i,1] = year of first row of data
    # data[i,4] = mean (estimate) of first row of data
  # For example n_sight[3,4] is the mean sightability for the third observation
  
  ##### Abundance #####
  # Estimates
  for(i in 1:nn_ca){
    n_sight_ca[i,4] ~ dnorm(N_c[n_sight_ca[i,1]], n_sight_ca[i,5])T(0,)
  }
  for(i in 1:nn_af){
    n_sight_af[i,4] ~ dnorm(N_f[n_sight_af[i,1]], n_sight_af[i,5])T(0,)
  }
  for(i in 1:nn_am){
    n_sight_am[i,4] ~ dnorm(N_m[n_sight_am[i,1]], n_sight_am[i,5])T(0,)
  }
  
  # Counts
  sd_afcount ~ dunif(0,50)
  tau_afcount <- 1/sd_afcount^2
  for(i in 1:nn_fc){
    af_count[i,4] ~ dnorm(N_f[af_count[i,1]], tau_afcount)T(0,)
  }
  
  sd_amcount ~ dunif(0, 50)
  tau_amcount <- 1/sd_amcount^2
  for(i in 1:nn_mc){
    am_count[i,4] ~ dnorm(N_m[am_count[i,1]], tau_amcount)T(0,)
  }
  
  ##### Survival #####
  # CJS - Feedgrounds
  for(i in 1:ns){
    s_cjs[i,4] ~ dnorm(S[s_cjs[i,2], s_cjs[i,3], s_cjs[i,1]], s_cjs[i,5])T(0,1)
  }
  
  ##### Recruitment #####
  # Ratio - Feedgrounds
  for(i in 1:nr){
    r_ratio[i,4] ~ dnorm(R[r_ratio[i,1]], r_ratio[i,5])T(0,1)
  }
  
  #Priors and GLMs==============================================================
  
  ##### Recruitment #####
  
  sd_R ~ dunif(0,50)
  tau_R <- 1/sd_R^2
  R_B0 ~ dlogis(0, 1)
  
  for(t in 1:nyr){ # t (year)
    R_yr[t] ~ dnorm(0, tau_R)
    logit(R[t]) <- R_B0 + R_yr[t]
  } # yr
  
  
  ##### Survival #####
  # Independent calf and adult survival
  # Sex effect on non-calves, sex survival equal in calves
  # Separate random effect on each sex/age class
    # _C_ = calf, survival from 0.5 to 1.5
    # _F_ = female, _M_ = male

  S_C_B0_ps ~ dunif(0, 1)
  S_F_B0_ps ~ dunif(0, 1)
  S_M_B0_ps ~ dunif(0, 1)
  
  S_C_B0 <- logit(S_C_B0_ps)
  S_F_B0 <- logit(S_F_B0_ps)
  S_M_B0 <- logit(S_M_B0_ps)
 
  sd_S_C ~ dunif(0,50)
  sd_S_F ~ dunif(0,50)
  sd_S_M ~ dunif(0,50)
  
  tau_S_C <- 1/sd_S_C^2
  tau_S_F <- 1/sd_S_F^2
  tau_S_M <- 1/sd_S_M^2
  
  for(t in 1:nyr){ # t (year)
    S[1,1,t] <- 0
    S[1,2,t] <- 0
    
    S_C_yr[t] ~ dnorm(0, tau_S_C)
    logit(S[2,1,t]) <- S_C_B0 + S_C_yr[t]
    S[2,2,t] <- S[2,1,t]
    
    S_F_yr[t] ~ dnorm(0, tau_S_F)
    S_M_yr[t] ~ dnorm(0, tau_S_M)
    logit(S[3,1,t]) <- S_F_B0 + S_F_yr[t]
    logit(S[3,2,t]) <- S_M_B0 + S_M_yr[t]
    S[4,1,t] <- S[3,1,t]
    S[4,2,t] <- S[3,2,t]
    
    # Derived for tracking
    survival_af[t] <- S[3,1,t]
    survival_am[t] <- S[3,2,t]
    survival_ca[t] <- S[2,1,t]
  } # t (year)
  
  ##### Abundance -- starting #####

  for(x in 1:2){ # x
    for(a in 1:4){ # a
      init_N[a,x] ~ dnorm(100, 0.0001)T(1,)
      N[a,x,1] <- init_N[a,x]
    } # a
  } # x
  
  # Derived Values for easier tracking
  N_c [1]  = N[1,1,1] + N[1,2,1]
  N_yf[1]  = N[2,1,1]
  N_ym[1]  = N[2,2,1]
  N_af[1]  = N[3,1,1] + N[4,1,1]
  N_am[1]  = N[3,2,1] + N[4,2,1]
  N_f[1]   = N[3,1,1] + N[4,1,1] + N[2,1,1]
  N_m[1]   = N[3,2,1] + N[4,2,1] + N[2,2,1]
  N_tot[1] = sum(N[1:4,1:2,1])
}